buildscript {
    ext {
        kotlinVersion = '1.3.11'
    }

    ext.deps = [
            kotlinRuntime: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",

            junit: 'junit:junit:4.12',
            testRunner: 'com.android.support.test:runner:1.0.2',
            testRules: 'com.android.support.test:rules:1.0.2',
            espressoCore: 'com.android.support.test.espresso:espresso-core:3.0.2',
            mockitoAndroid: 'org.mockito:mockito-android:2.21.0',

    ]

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.android.tools.build:gradle:3.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    configureAndroidPlugin(it)
    configureKotlinLint(it)
}

def configureAndroidPlugin(Project project) {
    def closure = {
        project.android {
            compileSdkVersion 28

            defaultConfig {
                minSdkVersion 15
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            packagingOptions {
                exclude 'META-INF/*.kotlin_module'
                exclude 'kotlin/**'
                exclude 'META-INF/*.version'
            }
        }
    }

    project.plugins.withId('com.android.application', closure)
    project.plugins.withId('com.android.library', closure)
}

static def configureKotlinLint(Project project) {
    def closure = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.29.0'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-r', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin-android', closure)
    project.plugins.withId('kotlin', closure)
}